<html>

<head>
<title>WebGL Sandbox</title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1"/>

<script type="text/javascript" src="gl-matrix-min.js"></script>
<script type="text/javascript" src="webgl.js"></script>
<script type="text/javascript" src="bullet-storm-const.js"></script>
<script type="text/javascript" src="bullet-storm-primitives.js"></script>
<script type="text/javascript" src="bullet-storm-input.js"></script>    
<script type="text/javascript" src="bullet-storm-engine.js"></script>
<script type="text/javascript" src="bullet-storm-player.js"></script>
<script type="text/javascript" src="bullet-storm-enemy.js"></script>
<script type="text/javascript" src="bullet-storm.js"></script>


<!-- =========================================== -->
<!--             BACKGROUND SHADERS              -->
<!-- =========================================== -->

<script id="bs.background.vs" type="x-shader/x-vertex">
    attribute vec3 a_VertexPosition;    
    
    void main(void) 
    {
        gl_Position = vec4(a_VertexPosition, 1.0);
    }
</script>

<script id="bs.blit.vs" type="x-shader/x-vertex">
    attribute vec3 a_VertexPosition;    
    attribute vec2 a_TexCoord;

    varying vec2 v_TexCoord;

    void main(void) 
    {
        v_TexCoord = a_TexCoord;
        gl_Position = vec4(a_VertexPosition, 1.0);
    }
</script>

<script id="bs.background.fs" type="x-shader/x-fragment">
    #ifdef GL_ES
    precision mediump float;
    #endif

    uniform float u_Time;
    uniform vec2 u_Resolution;

    const mat2 m2 = mat2(0.8, -0.6, 0.6, 0.8); 

    float cosNoise(in vec2 pos)
    {
        return 0.7*(sin(pos.x) + sin(pos.y));
    }

    float map(in vec3 pos)
    {
        vec2 q = pos.xz * 0.6;
        float h = 1.0;
            
        float s = 0.5;
        for(int i = 0; i < 8; i++)
        {
           h += s * cosNoise(q);
           s *= 0.5;
           q = m2 * q * 1.6;
        }

        h *= 4.0;

        return pos.y - h;
    }

    vec3 calcNormal(in vec3 pos)
    {
        vec3 nor;
        vec2 e = vec2(0.01,0.00001);
        nor.x = map(pos + e.xyy) - map(pos - e.xyy);
        nor.y = map(pos + e.yxy) - map(pos - e.yxy);
        nor.z = map(pos + e.yyx) - map(pos - e.yyx);
        return normalize(nor);
    }

    void main()
    {
        vec2 p = gl_FragCoord.xy / u_Resolution.xy;
        vec2 q = -1.0 + 2.0*p;
        q.x *= u_Resolution.x / u_Resolution.y; 

        vec3 ro = vec3(0.0, 30.0, - u_Time);
        vec3 rd = normalize(vec3(q.x, q.y - 2.0, -1.0));

        vec3 col = vec3(0.7, 0.8, 1.0);

        float tmax = 90.0;
        float t = 0.0;

        for(int i = 0; i < 128; i++)
        {
            vec3 pos = ro + rd * t;
            float h = map(pos);
            if(h < 0.1 || t > tmax)
            {
                break; 
            } 
            
            t += h*0.3;
        }

        vec3 light = normalize(vec3(0.1, 0.1, -0.9));

        if(t < tmax)
        {
            vec3 pos = ro + t*rd;
            vec3 nor = calcNormal(pos);

            float dif = clamp (dot(nor,light), 0.0,1.0);
            vec3 lig = vec3(1.0, 1.0,0.0)*dif;
            lig += vec3(0.1,0.2,0.3)*nor.y *2.0;
            
            vec3 mate = vec3(0.7, 0.0, 0.3); 

            mate = mix(mate, vec3(0.2,0.0,0.7), smoothstep(0.7,0.9,nor.y));
            col = lig * mate;
            
            float fog = exp(-0.00001 * t * t * t);
            col += ((1.0 - fog) * vec3(0.7,0.2,1.0));
        }

        gl_FragColor = vec4(col, 1.0);
    }
</script>

<!-- =========================================== -->
<!--                  SCENE SHADERS              -->
<!-- =========================================== -->

<script id="bs.default.vs" type="x-shader/x-vertex">
    attribute vec3 a_VertexPosition;
    attribute vec2 a_TexCoord;

    uniform mat4 u_ViewMatrix;
    uniform mat4 u_ModelMatrix;
    uniform mat4 u_ProjMatrix;
    uniform vec4 u_TintColor;

    varying vec4 v_Color;
    varying vec2 v_TexCoord;

    void main(void) 
    {
        mat4 mvpMatrix = u_ProjMatrix * u_ViewMatrix * u_ModelMatrix;
        gl_Position = mvpMatrix * vec4(a_VertexPosition, 1.0);
        v_Color = u_TintColor;
        v_TexCoord = a_TexCoord;
    }
</script>

<script id="bs.special-weapon.fs" type="x-shader/x-fragment">
    precision mediump float;

    varying vec2 v_TexCoord;
    uniform float u_Time;
     
    float hash( float n ) { return fract(sin(n)*753.5453123); }
     

    float noise( in vec2 x )
    {
        vec2 p = floor(x);
        vec2 f = fract(x);
        f = f*f*(3.0-2.0*f);
        
        float n = p.x + p.y*157.0;
        return mix(
                mix( hash(n+  0.0), hash(n+  1.0),f.x),
                mix( hash(n+157.0), hash(n+158.0),f.x),
                f.y);
    }
     
    float fbm(vec2 p, vec3 a)
    {
         float v = 0.0;
         v += noise(p * a.x) * 0.5;
         v += noise(p * a.y) * 0.25;
         v += noise(p * a.z) * 0.125;
         return v;
    }
     
    vec3 drawLines(vec2 uv, vec3 fbmOffset, vec3 color1, vec3 color2)
    {
        float timeVal = u_Time * -0.1;
        vec3 finalColor = vec3(0.0);

        for( int i=0; i < 3; ++i )
        {
            float indexAsFloat = float(i);
            float amp = 40.0 + indexAsFloat * 1.0;
            float period = 2.0 + indexAsFloat + 2.0;
            float thickness = mix(0.9, 1.0, noise(uv * 10.0));
            float t = abs(0.9 / (sin(uv.x + fbm(uv + timeVal * period, fbmOffset)) * amp) * thickness);
            
            finalColor += t * color1;
        }
        
        for(int i = 0; i < 5; ++i)
        {
            float indexAsFloat = float(i);
            float amp = 70.0 + indexAsFloat * 12.0;
            float period = 0.5 + indexAsFloat + 2.0;
            float thickness = mix(0.7, 2.0, noise(uv*10.0));
            float t = abs(0.8 / (sin(uv.x + fbm(uv + timeVal * period, fbmOffset)) * amp) * thickness);
            
            finalColor += t * color2 * 0.6;
        }
        
        return finalColor;
    }
     
    void main( void ) 
    {     
        vec2 uv = v_TexCoord.xy + vec2(-1.0, 0.0);
     
        vec3 lineColor1 = vec3(1.0, 0.5, 0.5);
        vec3 lineColor2 = vec3(0.3, 0.5, 0.5);
        
        vec3 finalColor = vec3(0);
        
        float t = sin(u_Time) * 0.5 + 0.5;
        float pulse = mix(0.10, 0.20, t);
        
        finalColor += drawLines(uv, vec3(1.0, 20.0, 30.0), lineColor1, lineColor2) * pulse;
        finalColor += drawLines(uv, vec3(1.0, 20.0, 4.0), lineColor1, lineColor2);
        
        gl_FragColor = vec4(finalColor, 1.0);
    }
</script>

<script id="bs.flatcolor.fs" type="x-shader/x-fragment">
    precision mediump float;

    varying vec4 v_Color;

    void main(void) 
    {
        gl_FragColor = v_Color;
    }
</script>

<script id="bs.blit.fs" type="x-shader/x-fragment">
    precision mediump float;

    varying vec2 v_TexCoord;

    uniform sampler2D u_Texture;

    void main(void) 
    {
        gl_FragColor = texture2D(u_Texture, vec2(v_TexCoord.s, v_TexCoord.t));
    }
</script>

<script id="bs.bullet.fs" type="x-shader/x-fragment">
    precision mediump float;

    varying vec4 v_Color;
    varying vec2 v_TexCoord;

    void main(void) 
    {
        float a = 1.0 - length(vec2(1.0) - v_TexCoord.xy * 2.0);

        gl_FragColor = vec4(v_Color.rgb, a);
    }
</script>


<link rel="stylesheet" type="text/css" href="styles.css">

</head>

<body onload="BulletStorm.run();">
	<canvas id="webgl-canvas" style="border: none;" width="1024" height="512"/>
</body>

</html>